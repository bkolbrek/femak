      SUBROUTINE EXLINM(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VKE1,
     1    VIE,VIE1,VFE,VSRE,VKGS,VKGD,VKGI,VFG,VCORG,KDLNC,KNEQ,VRES,
     2    VDLE,VNDSRC,KTYPRSG,VPRSG)
C***********************************************************************
C     EXLINM.FOR
C***********************************************************************
C     2/1-1995
C     NTH
C     Ulf R. Kristiansen & Tonni F. Johansen
C     Programmed after Dhatt og Touzot p.408
C***********************************************************************
C     TO EXECUTE BLOCK 'LINM'
C     ASSEMBLE AND SOLVE A LINEAR PROBLEM IN CORE
C***********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)

      include 'femak.cmn'
C  $DECLARE
      COMPLEX*16 VIE(1),VIE1(1),VSRE(1)
      REAL*8 VDIMP(1),VCORE(1),VPRNE(1),VPREE(1),VKE(1),VFE(1),VDLE(1)
      REAL*8 VRES(1),VKE1(1),VKGS(1),VKGD(1),VKGI(1),VFG(1)
      INTEGER KLD(1),KLOCE(1),KNE(1),KDLNC(1)

      COMPLEX*16 VNDSRC,VPRSG(1)
      INTEGER KTYPRSG(1)
      
      REAL*8 ENERG
      INTEGER I
C----- ------------------------------------------------------------------
      REWIND M3
C
C-------- ASSEMBLE KG
C
C-------- SAVE UNMODIFIED VECTOR FG (BY B.C.) ON FILE M3
      WRITE(M3)  (VFG(I),I=1,NEQ)
      IF(M.GE.2) WRITE(MP,2000) (VFG(I),I=1,NEQ)
2000  FORMAT(/' GLOBAL LOAD VECTOR NON MODIFIED BY B.C. (FG)'
     1/(1X,10E12.5))
C--------  ASSEMBLE KG, MODIFY FG FOR THE B.C. AND SAVE THEM
      CALL ASKG(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VKE1,
     1   VIE,VIE1,VFE,VKGS,VKGD,VKGI,VFG,VDLE,VRES,
     2   VNDSRC,KTYPRSG,VPRSG)
      WRITE(M3) (VFG(I),I=1,NEQ)
      WRITE(M3) (VKGS(I),I=1,NKG),(VKGD(I),I=1,NEQ)
      IF(NSYM.EQ.1) WRITE(M3) (VKGI(I),I=1,NKG)
C--------  PRINT KG AND FG
      IF(M.GE.2) THEN
        WRITE(MP,2005) (VKGS(I),I=1,NKG)
        WRITE(MP,2010) (VKGD(I),I=1,NEQ)
        IF(NSYM.EQ.1) WRITE(MP,2020) (VKGI(I),I=1,NKG)
        WRITE(MP,2030) (VFG(I),I=1,NEQ)
      ENDIF
2005  FORMAT(/' GLOBAL MATRIX (KG)'/'     UPPER TRIANGLE'/
     1    (1X,10E12.5))
2010  FORMAT('    DIAGONAL'/(1X,10E12.5))
2020  FORMAT('    LOWER TRIANGLE'/(1X,10E12.5))
2030  FORMAT(/' GLOBAL LOAD VECTOR MODIFIED BY THE B.C. (FG)'
     1 /(1X,10E12.5))
C
C--------   SOLVE
C
      CALL SOL(VKGS,VKGD,VKGI,VFG,KLD,NEQ,MP,1,1,NSYM,ENERG)
      IF(NSYM.NE.1) WRITE(MP,2035) ENERG
2035  FORMAT(15X,'ENERGY    (ENERG)=',1E12.5)
      IF(M.GE.2) THEN
        WRITE(MP,2040) (VKGS(I),I=1,NKG)
        WRITE(MP,2010) (VKGD(I),I=1,NEQ)
        IF(NSYM.EQ.1) WRITE(MP,2020) (VKGI(I),I=1,NKG)
      ENDIF
2040  FORMAT(/' TRIANGULARIZED MATRIX (KG)'/'    UPPER TRIANGLE'/
     1  (1X,10E12.5))

C-------- PIVOTS OF KG AND DETERMINANT
      CALL PRPVTS(VKGD)
C-------- EVALUATE AND PRINT RESIDUAL VECTOR K.U - F
      IF(NRES.EQ.1) CALL PRRESD(VKGS,VKGD,VKGI,VFG,KLD,VRES)
C-------- PRINT THE SOLUTION
      WRITE(MP,2050)
2050  FORMAT(//' SOLUTION'//)
      CALL PRSOL(KDLNC,VCORG,VDIMP,KNEQ,VFG)
C
C--------  EVALUATE AND PRINT GRADIENTS (STRESSES)
C
      CALL ASGRAD(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VKE1,
     1  VIE,VIE1,VFE,VSRE,VKGS,VKGD,VKGI,VFG,VDLE,VRES,VNDSRC,
     2  KTYPRSG,VPRSG)      
C
C------- EVALUATE AND PRINT EQUILIBRIUM RESIDUAL VECTOR
C
C------- READ VECTOR FG AND CHANGE SIGN
      REWIND M3
      READ(M3) (VRES(I),I=1,NEQ)
      DO I=1,NEQ
        VRES(I)=-VRES(I)
      ENDDO
      
C------- ASSEMBLE THE RESIDUALS
      CALL ASRESD(1,1,KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,
     1  KNE,VKE,VKE1,VIE,VIE1,VFE,VSRE,VKGS,VKGD,VKGI,VFG,VDLE,VRES,
     2  VNDSRC,KTYPRSG,VPRSG,VRES(NEQ+1))
C------- PRINT THE RESIDUALS
      WRITE(MP,2060)
2060  FORMAT(//' EQUILIBRIUM RESIDUALS AND REACTIONS'//)
      CALL PRSOL(KDLNC,VCORG,VRES(NEQ+1),KNEQ,VRES)
      
      RETURN
      END
